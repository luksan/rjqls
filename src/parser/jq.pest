WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* }

char = { !("\"" | "\\") ~ ANY  }
escape = { "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

program = _{ SOI ~ includes ~ pratt_expr ~ EOI }

includes = _{ (include_directive | module_directive)* }
include_directive = {"include"  ~ "\"" ~ inner_str ~ "\"" ~ pratt_expr? ~ ";"}
module_directive = {"module" ~ pratt_expr ~ ";"}
// func_def is a prefix in pratt_expr
func_def = {
    "def" ~ ident ~
    ( "(" ~ (ident | variable) ~ (";" ~ (ident | variable))*  ~ ")" )?
    ~ ":" ~ pratt_expr ~ ";"
}

// Only includes and func defs are allowed in a module, but parse an optional expr at the end to
// enable a nicer error message
jq_module = _{ SOI ~ includes ~ func_def* ~ pratt_expr? ~ EOI }
path_string = ${"\"" ~ inner_str ~ "\""}

included_src = _{ SOI ~ (pratt_expr | func_def*) ~ eoi }
eoi = _{ !ANY } // Silent EOI

/// Everything below is for pratt parsing of filter expressions

plusminus = _{"+" | "-" }

number = @{ "-"? ~ ASCII_DIGIT+ ~ ( "." ~ ASCII_DIGIT*)? ~ ( ("e"|"E") ~ plusminus? ~ ASCII_DIGIT+)? }

string = ${ "\"" ~ (inner_str | str_interp)* ~ "\"" }
inner_str = ${ (char|escape)+ }
str_interp = !{ "\\(" ~ pratt_expr ~ ")" }

null = { "null" }
bool = { "true" | "false" }
literal = { number | bool | null } // string is parsed separately

ident = ${ !(keywords ~ !( ASCII_ALPHANUMERIC | "_")) ~  (ASCII_ALPHA|"_") ~ ( ASCII_ALPHANUMERIC | "_" )* }
keywords = { "and"|"as"|"break"|"catch"|"foreach"|"if"|"then"|"else"|"elif"|"end"|"label"|"or"|"reduce"|"try"|"import"|"include"|"module" }

obj = { "{" ~ (obj_pair ~ ("," ~ obj_pair)*)? ~ "}" }

obj_pair = { obj_key ~ ":" ~ obj_val }
obj_key = { ident | string | variable | ("(" ~ pratt_expr ~ ")") }
obj_val = { "(" ~ pratt_expr ~ ")" | obj_val_nobrackets }
// only pipe is allowed as infix op without brackets in object construction
obj_val_nobrackets = _{ prefix* ~ primary ~ postfix* ~ (pipe ~ prefix* ~ primary ~ postfix* )* }
arr = {"[" ~ pratt_expr? ~ "]"}

variable = ${ "$" ~ ident }

pratt_prog = { SOI ~ pratt_expr ~ EOI }
pratt_expr = { ppp ~ (infix ~ ppp)* }
  ppp = _{ prefix * ~ primary ~ postfix* }
  infix = _{ arith_assign | upd_assign| alt | and | or | ord | eq | neq | assign | add | sub| mul | div | mod_ | comma | pipe }
    alt = {"//"}
    eq = { "==" }
    neq = { "!=" }
    add = { "+" }
    sub = { "-" }
    mul = { "*" }
    div = { "/" }
    mod_ = { "%" }
    ord = { "<=" | ">=" | "<"| ">"}
    and = {"and"}
    or = {"or"}
    upd_assign = {"|="}
    assign = {"="}
    arith_assign = ${ (alt | add | sub | mul | div | mod_ ) ~ "=" }
    comma = { "," }
    pipe = { "|" }
  prefix = _{ label | dbg_brk_pre }
    dbg_brk_pre = { "ยง" }
    label = _{ pipe_label ~ labeled_pipe } // A label has to generate both a primary and an infix token from the same source span
      pipe_label = { "label" ~ variable ~ "|" } // Dot in the AST
      labeled_pipe = { &ANY } // pratt parsed as infix
  postfix = _{ idx_chain+ | try_postfix | dbg_brk_post | as_ }
    idx_chain = _{ "."? ~ idx_kinds ~ try_idx? }
    idx_kinds = _{ index | iterate | slice }
    try_idx = _{ idx_chain_pipe ~ idx_chain_dot ~ idx_kinds ~ try_postfix }
    idx_chain_pipe = { "."? ~ &"[" | "." } // synthetic |. between indexing ops
    idx_chain_dot = { &ANY }
    index = { "[" ~ pratt_expr ~ "]" | ident | string }
    iterate = { "[" ~ "]" }
    slice = { "[" ~ (( pratt_expr ~ colon ~ pratt_expr? ) | ( colon ~ pratt_expr )) ~ "]" }
    colon = { ":" }
    try_postfix = { "?"+ }
    dbg_brk_post = { "ยง" }
    as_ = { "as" ~  variable_binding ~ "|" ~ pratt_expr }
    variable_binding = { variable | arr | obj } // used for destructuring variable binding
  primary = _{
    obj |
    if_cond |
    label |
    break_ |
    foreach|
    func_scope |
    reduce |
    try_catch|
    dot_primary |
    literal |
    call |
    string |
    variable |
    arr |
    primary_group
  }
    dot_primary = { "." }
    call = { ident ~ ("(" ~ pratt_expr ~ (";" ~ pratt_expr)* ~ ")")? }
    primary_group = { "(" ~ pratt_expr ~ ")" }
    func_scope = { func_def+ ~ pratt_expr }
    if_cond = { "if" ~ pratt_expr ~ "then" ~ pratt_expr ~ elif* ~ else_? ~ "end" }
    elif = {"elif" ~ pratt_expr ~ "then" ~ pratt_expr}
    else_ = {"else" ~ pratt_expr}
    break_ = {"break"~ variable}
    foreach = {"foreach" ~ pratt_expr ~ "as" ~ variable ~ "(" ~ pratt_expr ~ ";" ~ pratt_expr ~ (";" ~ pratt_expr)? ~ ")"}
    reduce = {"reduce" ~ pratt_expr ~ "as" ~ variable ~ "(" ~ pratt_expr ~ ";" ~ pratt_expr ~ ")"}
    try_catch = {"try" ~pratt_expr ~ ("catch" ~ pratt_expr)?}


